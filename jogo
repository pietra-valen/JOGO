programa {
    inclua biblioteca Util --> u
    inclua biblioteca Texto --> t

    // Inventário e Status
    logico temArmadura = falso
    logico temBastao = falso
    logico temChakram = falso
    logico chakramUsado = falso
    logico pergaminho = falso

    // Vida e danos
    inteiro vidaJogador = 300
    inteiro danoJogador = u.sorteia(10, 20)
    logico ajudadoPorKhonshu = falso
    logico ajudaLayla = falso
    logico ajudaTawret = falso
    
    // Variáveis auxiliares
    cadeia simNao
    cadeia companhia
    inteiro escolhaNumerica

    funcao inicio() {
    	
        introducao_historia()
        limpa()
        
        jogo_escolhas()
    }

    funcao introducao_historia() {
        escreva("======================================================\n")
        escreva("             LUA E SOMBRAS - CAVALEIRO DA LUA         \n")
        escreva("======================================================\n\n")

        escreverLento("Você é Marc Spector, um homem atormentado por múltiplas personalidades – Steven, Jake e ele mesmo.\n")
        escreverLento("Morto em combate, foi trazido de volta à vida como o avatar do deus egípcio Khonshu, tornando-se o Cavaleiro da Lua.\n\n")
        escreverLento("Khonshu o envia em uma missão urgente: impedir que a deusa Ammit desperte e julgue toda a humanidade antes mesmo que os crimes aconteçam.\n\n")
    }

    funcao jogo_escolhas() {
        cadeia escolher
        escreverLento("\nKhonshu surge no reflexo rachado do espelho, seu rosto esquelético se projetando.\n")
        escreverLento("(A)ceitar ou (R)esistir?\n")
        leia(escolher)

        se (escolher == "A" ou escolher == "a") {
            ajudadoPorKhonshu = verdadeiro
            escreverLento("Khonshu sorri, aprovando sua decisão.\n")
        } senao {
            escreverLento("Você resiste, mas Khonshu o força. Você sente dor (-10 vida).\n")
            vidaJogador -= 10
        }
        
        
        escolha_personalidade()
        
        escolha_companhia()
        
        caminho_itens()
        
        escreverLento("\nVocê segue em frente, mas no meio do caminho encontra Arthur Harrow o avatar de Ammit...\n")
        
        batalhaHarrow()

        escreverLento("\nApós derrotar Harrow, seu corpo está exausto, mas a missão ainda não terminou...\n")
        
        missao_balanca()
        
        caminho_memorias()
        
        escreverLento("\nPronto para os desafios finais, você respira fundo e segue adiante.\n")
    }

    funcao escolha_personalidade() {
        cadeia personagens[3] = {"Marc Spector", "Steven Grant", "Jake Lockley"}
        inteiro escolhaNumerica

        escreverLento("Escolha com qual personalidade deseja seguir: \n")
        para (inteiro i = 0; i < 3; i++) {
            escreva("[", i, "] ", personagens[i], "\n")
        }
        
        leia(escolhaNumerica)

        se (escolhaNumerica == 0) {
            escreverLento("Você escolheu Marc Spector!\n")
        } senao se (escolhaNumerica == 1) {
            escreverLento("Você escolheu Steven Grant!\n")
        } senao se (escolhaNumerica == 2) {
            escreverLento("Você escolheu Jake Lockley!\n")
        } senao {
            escreverLento("Escolha inválida. Seguindo com Marc Spector.\n")
        }
    }
    
    funcao escolha_companhia() {
        cadeia companhias[3] = {"Taweret", "Layla", "Seguir sozinho"}

        escreva("\nEscolha sua companhia:\n")
        para (inteiro i = 0; i < 3; i++) {
            escreva("[", i, "] ", companhias[i], "\n")  
        }

        leia(escolhaNumerica)

        se (escolhaNumerica == 0) {
            ajudaTawret = verdadeiro
            escreverLento("Taweret se junta a você e voces seguem juntos na joranada.\n")
        } senao se (escolhaNumerica == 1) {
            ajudaLayla = verdadeiro
            escreverLento("Layla se junta a você e voces seguem juntos na joranada.\n")
        } senao {
            escreverLento("Você segue sozinho.\n")
        }
    }

    funcao caminho_itens() {
        cadeia itens[4] = {"Salão Lunar", "Caverna dos ossos", "Torre Celestial", "Templo de Areia"}

        escreva("\nEscolha um local para se equipar e realizar a missao com facilidade:\n")
        para (inteiro i = 0; i < 4; i++) {
            escreva("[", i, "] ", itens[i], "\n")  
        }

        leia(escolhaNumerica)

        se (escolhaNumerica == 0) {
            temArmadura = verdadeiro
            escreverLento("Você recebeu a Armadura Lunar.\n")
        } senao se (escolhaNumerica == 1) {
            temBastao = verdadeiro
            escreverLento("Você recebeu o Bastão Lunar.\n")
        } senao se (escolhaNumerica == 2) {
            temChakram = verdadeiro
            escreverLento("Você recebeu o Chakram Lunar.\n")
        } senao se (escolhaNumerica == 3) {
            pergaminho = verdadeiro
            escreverLento("Você recebeu o Pergaminho com a fraqueza de Ammit.\n")
        } senao {
            escreverLento("Escolha inválida. Você não encontra nenhum item.\n")
        }
    }

    funcao escreverLento(cadeia palavra) {
        inteiro quant = t.numero_caracteres(palavra)
        para (inteiro i = 0; i < quant; i++) {
            escreva(t.obter_caracter(palavra, i))
            u.aguarde(10)
        }
    }

    funcao batalhaHarrow() {
        inteiro vida_Harrow = 120
        inteiro danoHarrow

        enquanto (vida_Harrow > 0 e vidaJogador > 0) {
            escreva("\nEscolha: [A] atacar | [D] defender")
            se (ajudaTawret) { 
                escreva(" | [T] pedir ajuda a Taweret")
            }
            escreva("\n")

            cadeia escolher
            leia(escolher)
            
            se (escolher == "A" ou escolher == "a") {
                danoJogador = u.sorteia(10, 20)
                se (temBastao) {
                    danoJogador += 5
                    escreverLento("Você usa o Bastão Lunar.\n")
                }
                vida_Harrow -= danoJogador

                danoHarrow = u.sorteia(5, 10)
                se (temArmadura) {
                    danoHarrow -= 5
                    se (danoHarrow < 0) {
                        danoHarrow = 0
                    }
                }
                vidaJogador -= danoHarrow
                escreva("Você recebe ", danoHarrow, " de dano. E Harrow recebe ", danoJogador, " de dano\n")

                se (temChakram e chakramUsado == falso) {
                    escreverLento("Usar Chakram agora? (S/N)\n")
                    leia(simNao)
                    se (simNao == "S" ou simNao == "s") {
                        vida_Harrow -= 30
                        chakramUsado = verdadeiro
                        escreverLento("Chakram lançado! -30 de vida do inimigo.\n")
                    }
                }
            } senao se (escolher == "D" ou escolher == "d") {
                danoHarrow = u.sorteia(5, 10)
                se (temArmadura) {
                    danoHarrow -= 5
                    se (danoHarrow < 0) {
                        danoHarrow = 0
                    }
                }
                vidaJogador -= danoHarrow
                escreva("Você se defende e recebe ", danoHarrow, " de dano. E Harrow recebe ", danoJogador, " de dano\n")
            } senao se (escolher == "T" e ajudaTawret) {
                inteiro sorte = u.sorteia(1, 2)
                se (sorte == 1) {
                    vidaJogador += 20
                    se (vidaJogador > 300) {
                        vidaJogador = 300
                    }
                    escreverLento("Taweret cura você! +20 vida.\n")
                } senao {
                    escreverLento("Taweret bloqueia o ataque inimigo.\n")
                }
            }

            se (vida_Harrow < 0) { vida_Harrow = 0 }
            se (vidaJogador < 0) { vidaJogador = 0 }

            escreva("Sua vida: ", vidaJogador, " | Vida do inimigo: ", vida_Harrow, "\n")
            u.aguarde(800)
        }

        se (vida_Harrow <= 0) {
            escreverLento("Harrow cai. Vitória!\n")
        } senao {
            escreverLento("Você foi derrotado. Fim de jogo.\n")
            escreverLento("Sua jornada como Cavaleiro da Lua chegou ao fim.")
        }
    }

    funcao missao_balanca() {
        escreverLento("\nVocê é levado à balança de Osíris...\n")
        u.aguarde(50)

        inteiro pontos = 0
        cadeia resposta

        escreverLento("\nPergunta 1: Luta por si ou pelos outros? (S/O)\n")
        leia(resposta)
        se (resposta == "O" ou resposta == "o") {
            pontos++
            escreverLento("A balança se move a seu favor.\n")
        } senao {
            escreverLento("A balança se inclina contra você.\n")
        }

        escreverLento("\nPergunta 2: Aceita todas as partes de você? (S/N)\n")
        leia(resposta)
        se (resposta == "S" ou resposta == "s") {
            pontos++
            escreverLento("As conchas brilham em harmonia.\n")
        } senao {
            escreverLento("A concha esquerda afunda, revelando um peso oculto.\n")
        }

        se (pontos >= 2) {
            vidaJogador += 30
            se (vidaJogador > 300) { vidaJogador = 300 }
            escreverLento("A balança se equilibra. (+30 vida)\n")
        } senao {
            vidaJogador -= 20
            escreverLento("A balança se inclina contra você. (-20 vida)\n")
        }

        u.aguarde(100)
        escreverLento("\nA luz da balança se dissipa, revelando três portas adiante...\n")
    }

    funcao caminho_memorias() {
        cadeia portas[3] = {"Casa simples — ecos da infancia", "Templo em ruínas — sangue derramado nas areias do Egito.", "Corredor sombrio — sombras internas."}
        inteiro efeitosVida[3] = {-10, +40, -15}
        inteiro efeitosDano[3] = {5, 0, 10}
        
        escreva("\nEscolha uma porta:\n")
        para (inteiro i = 0; i < 3; i++) {
            escreva("[", i, "] ", portas[i], "\n")  
        }
        
        leia(escolhaNumerica) 
        
        se (escolhaNumerica >= 0 e escolhaNumerica < 3) {
            vidaJogador += efeitosVida[escolhaNumerica]
            danoJogador += efeitosDano[escolhaNumerica]
            escreverLento("Você atravessa a porta escolhida. Vida atual: " + vidaJogador + ", Dano permanente: " + danoJogador + "\n")
        } senao {
            escreverLento("Escolha inválida. Você fica no limbo, sua mente está em guerra. (-10 vida)\n")
            vidaJogador -= 10
        }
    }
}
