programa {
    inclua biblioteca Util --> u
    inclua biblioteca Texto --> t

    // Inventário
    logico temArmadura = falso
    logico temBastao = falso
    logico temChakram = falso
    logico chakramUsado = falso
    logico pergaminho = falso

    // Vida e danos
    inteiro vidaJogador = 300
    inteiro danoJogador = u.sorteia(10, 20)
    logico ajudadoPorKhonshu = falso
    logico ajudaLayla = falso
    logico ajudaTawret = falso

    cadeia simNao
    cadeia companhia

    funcao inicio() {
        introducao_historia()
        limpa()
        jogo_escolhas()
    }

    funcao introducao_historia() {
        escreva("======================================================\n")
        escreva("            LUA E SOMBRAS - CAVALEIRO DA LUA         \n")
        escreva("======================================================\n\n")

        escreverLento("Você é Marc Spector, um homem atormentado por múltiplas personalidades – Steven, Jake e ele mesmo.\n")
        escreverLento("Morto em combate, foi trazido de volta à vida como o avatar do deus egípcio Khonshu, tornando-se o Cavaleiro da Lua.\n\n")
        escreverLento("Khonshu o envia em uma missão urgente: impedir que a deusa Ammit desperte e julgue toda a humanidade antes mesmo que os crimes aconteçam.\n\n")
    }

    funcao jogo_escolhas() {
        cadeia escolher
        escreverLento("\nKhonshu surge no reflexo rachado do espelho, seu rosto esquelético se projetando.")
        escreverLento("(A)ceitar ou (R)esistir?\n")
        leia(escolher)

        se (escolher == "A" ou escolher == "a") {
            ajudadoPorKhonshu = verdadeiro
            escreverLento("Khonshu sorri, aprovando sua decisão.\n")
        } senao {
            escreverLento("Você resiste, mas Khonshu o força. Você sente dor (-10 vida).\n")
            vidaJogador -= 10
        }

        escolha_personalidade()
        escolha_companhia()
        caminho_itens()

        escreverLento("\nVocê segue em frente, mas no meio do caminho encontra Arthur Harrow...\n")
        u.aguarde(1000)
        batalhaHarrow()

        escreverLento("\nApós derrotar Harrow, seu corpo está exausto, mas a missão ainda não terminou...\n")
        missao_balanca()
        caminho_memorias()
        escreverLento("\nPronto para os desafios finais, você respira fundo e segue adiante.\n")
    }

    funcao escolha_personalidade() {
        cadeia personagens[3] = {"Marc Spector", "Steven Grant", "Jake Lockley"}

        escreva("\nEscolha sua personalidade:\n")
        para (inteiro i = 0; i < 3; i++) {
            escreva("[", i+1, "] ", personagens[i], "\n")  // corrigido: não usar i++ aqui
        }

        cadeia escolhaPersonagem
        leia(escolhaPersonagem)

        se (escolhaPersonagem == "1") {
            escreverLento("Marc Spector selecionado.\n")
        } senao se (escolhaPersonagem == "2") {
            escreverLento("Steven Grant selecionado.\n")
        } senao {
            escreverLento("Jake Lockley selecionado.\n")
        }
    }

    funcao escolha_companhia() {
        cadeia companhias[3] = {"Taweret", "Layla", "Seguir sozinho"}

        escreva("\nEscolha sua companhia:\n")
        para (inteiro i = 0; i < 3; i++) {
            escreva("[", i+1, "] ", companhias[i], "\n")  // corrigido: não usar i++ aqui
        }

        leia(companhia)

        se (companhia == "1") {
            ajudaTawret = verdadeiro
            escreverLento("Taweret se junta a você.\n")
        } senao se (companhia == "2") {
            ajudaLayla = verdadeiro
            escreverLento("Layla se junta a você.\n")
        } senao {
            escreverLento("Você segue sozinho.\n")
        }
    }

    funcao caminho_itens() {
        cadeia itens[4] = {"Salão Lunar", "Caverna dos ossos", "Torre Celestial", "Templo de Areia"}

        escreva("\nEscolha o local para pegar os itens:\n")
        para (inteiro i = 0; i < 4; i++) {
            escreva("[", i+1, "] ", itens[i], "\n")  // corrigido
        }

        inteiro opcao
        leia(opcao)

        se (opcao == 1) {
            temArmadura = verdadeiro
            escreverLento("Você recebeu a Armadura Lunar.\n")
        } senao se (opcao == 2) {
            temBastao = verdadeiro
            escreverLento("Você recebeu o Bastão Lunar.\n")
        } senao se (opcao == 3) {
            temChakram = verdadeiro
            escreverLento("Você recebeu o Chakram Lunar.\n")
        } senao se (opcao == 4) {
            pergaminho = verdadeiro
            escreverLento("Você recebeu o Pergaminho com a fraqueza de Ammit.\n")
        }
    }

    funcao escreverLento(cadeia palavra) {
        inteiro quant = t.numero_caracteres(palavra)
        para (inteiro i = 0; i < quant; i++) {
            escreva(t.obter_caracter(palavra, i))
            u.aguarde(10)
        }
    }

    funcao batalhaHarrow() {
        inteiro vida_Harrow = 120
        inteiro danoHarrow

        enquanto (vida_Harrow > 0 e vidaJogador > 0) {
            escreva("\nEscolha: [A] atacar | [D] defender")
            se (companhia == "1") {
                escreva(" | [T] pedir ajuda a Taweret")
            }
            escreva("\n")

            cadeia escolher
            leia(escolher)

            se (escolher == "A" ou escolher == "a") {
                danoJogador = u.sorteia(10, 20)
                se (temBastao) {
                    danoJogador += 5
                    escreverLento("Você usa o Bastão Lunar.\n")
                }
                vida_Harrow -= danoJogador

                danoHarrow = u.sorteia(5, 10)
                se (temArmadura) {
                    danoHarrow -= 5
                    se (danoHarrow < 0) {
                        danoHarrow = 0
                    }
                }
                vidaJogador -= danoHarrow
                escreva("Você recebe ", danoHarrow, " de dano.\n")

                se (temChakram e chakramUsado == falso) {
                    escreverLento("Usar Chakram agora? (S/N)\n")
                    leia(simNao)
                    se (simNao == "S" ou simNao == "s") {
                        vida_Harrow -= 30
                        chakramUsado = verdadeiro
                        escreverLento("Chakram lançado! -30 de vida do inimigo.\n")
                    }
                }
            } senao se (escolher == "D" ou escolher == "d") {
                danoHarrow = u.sorteia(5, 10)
                se (temArmadura) {
                    danoHarrow -= 5
                    se (danoHarrow < 0) {
                        danoHarrow = 0
                    }
                }
                vidaJogador -= danoHarrow
                escreva("Você se defende e recebe ", danoHarrow, " de dano.\n")
            } senao se (escolher == "T" e companhia == "1") {
                inteiro sorte = u.sorteia(1, 2)
                se (sorte == 1) {
                    vidaJogador += 20
                    se (vidaJogador > 300) {
                        vidaJogador = 300
                    }
                    escreverLento("Taweret cura você! +20 vida.\n")
                } senao {
                    escreverLento("Taweret bloqueia o ataque inimigo.\n")
                }
            }

            se (vida_Harrow < 0) { vida_Harrow = 0 }
            se (vidaJogador < 0) { vidaJogador = 0 }

            escreva("Sua vida: ", vidaJogador, " | Vida do inimigo: ", vida_Harrow, "\n")
            u.aguarde(800)
        }

        se (vida_Harrow <= 0) {
            escreverLento("Harrow cai. Vitória!\n")
        }
    }

    funcao missao_balanca() {
        escreverLento("\nVocê é levado à balança de Osíris...\n")
        u.aguarde(50)

        inteiro pontos = 0
        cadeia resposta

        escreverLento("\nPergunta 1: Luta por si ou pelos outros? (S/O)\n")
        leia(resposta)
        se (resposta == "O" ou resposta == "o") {
            pontos++
            escreverLento("A balança se move a seu favor.\n")
        } senao {
            escreverLento("A balança se inclina contra você.\n")
        }

        escreverLento("\nPergunta 2: Aceita todas as partes de você? (S/N)\n")
        leia(resposta)
        se (resposta == "S" ou resposta == "s") {
            pontos++
            escreverLento("As conchas brilham em harmonia.\n")
        } senao {
            escreverLento("A concha esquerda afunda, revelando um peso oculto.\n")
        }

        se (pontos >= 2) {
            vidaJogador += 30
            se (vidaJogador > 300) { vidaJogador = 300 }
            escreverLento("A balança se equilibra. (+30 vida)\n")
        } senao {
            vidaJogador -= 20
            escreverLento("A balança se inclina contra você. (-20 vida)\n")
        }

        u.aguarde(100)
        escreverLento("\nA luz da balança se dissipa, revelando três portas adiante...\n")
    }

    funcao caminho_memorias() {
        cadeia portas[3] = {
            "Casa simples — ecos da infância.",
            "Templo em ruínas — sangue derramado nas areias do Egito.",
            "Corredor sombrio — sombras internas."
        }

        inteiro efeitosVida[3] = {-10, +40, -15}
        inteiro efeitosDano[3] = {5, 0, 10}

        escreva("\nEscolha uma porta:\n")
        para (inteiro i = 0; i < 3; i++) {
            escreva("[", i+1, "] ", portas[i], "\n")  // corrigido
        }

        inteiro escolhaPorta
        leia(escolhaPorta)
        escolhaPorta -= 1

        vidaJogador += efeitosVida[escolhaPorta]
        danoJogador += efeitosDano[escolhaPorta]

        escreverLento("Você atravessa a porta escolhida. Vida atual: " + vidaJogador + ", Dano permanente: " + danoJogador + "\n")
    }
}
